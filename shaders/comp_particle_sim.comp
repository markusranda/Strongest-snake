#version 450
layout(local_size_x = 64) in;

// DATASTRUCTURES
struct Particle {
    vec2 pos;
    vec2 vel;
    float life;
    uint alive;
};

// BINDINGS
layout(std430, binding = 0) readonly  buffer ParticlesIn  { Particle particlesIn[];  };
layout(std430, binding = 1) writeonly buffer ParticlesOut { Particle particlesOut[]; };
layout(std430, binding = 2) buffer Counters {
    // Counters
    uint inCount;       // ACTIVE count for particlesIn
    uint outCount;      // MUST be reset to 0 before this pass
    uint maxParticles;
    uint _pad;
    
    // DrawCmd
    uint vertexCount;
    uint instanceCount;
    uint firstVertex;
    uint firstInstance;
} counters;

// PUSH CONSTANT
layout(push_constant) uniform Params {
    float delta;
} pc;

void main() {
    uint id = gl_GlobalInvocationID.x;
    if (id >= counters.inCount) return;

    Particle p = particlesIn[id];

    // Update (assume input is active/dense, but keep alive flag if you want)
    p.vel.y += 512.0 * pc.delta;
    p.pos += p.vel * pc.delta;
    p.life -= 7.0 * pc.delta;

    if (p.life > 0.0) {
        p.alive = 1u;

        uint dst = atomicAdd(counters.outCount, 1u);
        if (dst < counters.maxParticles) {
            particlesOut[dst] = p;
        }
    } else {
        p.alive = 0u;
    }
}
